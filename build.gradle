plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'io.freefair.lombok' version '8.0.1'
}

group = 'mr.cookie'
version = '0.0.1'
sourceCompatibility = '17'

apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

ext {
    apacheCollectionsVersion = "4.4"
    assertjVersion = "3.24.2"
    checkstyleVersion = "0.0.38"
    flywayVersion = "9.17.0"
    jetBrainsAnnotationVersion = "24.0.1"
    lombokVersion = "1.18.26"
    mapStructVersion = "1.5.5.Final"
    mysqlConnectorVersion = "8.0.33"
    snakeyamlVersion = "2.0"
    springDocVersion = "2.1.0"
    springFrameworkVersion = "6.0.8"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.apache.commons:commons-collections4:${apacheCollectionsVersion}"
    implementation "org.jetbrains:annotations:${jetBrainsAnnotationVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    implementation "org.flywaydb:flyway-mysql:${flywayVersion}"

    // transitive dep
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "org.springframework:spring-expression:${springFrameworkVersion}"
    implementation "org.springframework:spring-webmvc:${springFrameworkVersion}"

    runtimeOnly "com.h2database:h2"

    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:${checkstyleVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
tasks.withType(Test).configureEach {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
    }
}
jacoco {
    toolVersion = "0.8.10"
}

sonarqube {
    properties {
        property "sonar.projectKey", "cookieMr_spring6udemy"
        property "sonar.organization", "cookiemr-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.withType(Checkstyle).configureEach {
    exclude "**/generated/**"
    exclude "**/sources/**"
}
checkstyle {
    toolVersion '10.10.0'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}
