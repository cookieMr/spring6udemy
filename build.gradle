plugins {
    id 'checkstyle'
    id 'io.freefair.lombok' version '8.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
    id 'java'
    id 'org.owasp.dependencycheck' version '8.4.0'
    id 'org.sonarqube' version '4.3.1.3277'
    id 'org.springframework.boot' version '3.1.3'
}

group = 'mr.cookie'
version = '0.0.2'
sourceCompatibility = '17'

apply from: "$rootDir/gradle/unitTest.gradle"
apply from: "$rootDir/gradle/integrationTest.gradle"
apply from: "$rootDir/gradle/quality.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

repositories {
    mavenCentral()
}

ext {
    apacheCollectionsVersion = "4.4"
    assertjVersion = "3.24.2"
    checkstyleVersion = "10.12.3"
    flywayVersion = "9.22.2"
    h2Version = "2.2.224"
    jetBrainsAnnotationVersion = "24.0.1"
    lombokVersion = "1.18.30"
    mapStructVersion = "1.5.5.Final"
    mysqlConnectorVersion = "8.0.33"
    snakeyamlVersion = "2.2"
    springDocVersion = "2.2.0"
    springFrameworkVersion = "6.0.12"
    tomcatVersion = "10.1.13"
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details -> {
        var dep = details.requested
        if (dep.group == 'org.apache.tomcat.embed') {
            details.useVersion("${tomcatVersion}")
        }
    }}
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.apache.commons:commons-collections4:${apacheCollectionsVersion}"
    implementation "org.jetbrains:annotations:${jetBrainsAnnotationVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    implementation "org.flywaydb:flyway-mysql:${flywayVersion}"

    // transitive dep
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "org.springframework:spring-expression:${springFrameworkVersion}"
    implementation "org.springframework:spring-webmvc:${springFrameworkVersion}"

    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "com.h2database:h2:${h2Version}"
}
