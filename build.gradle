plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.sonarqube' version '4.3.1.3277'
    id 'io.freefair.lombok' version '8.3'
}

group = 'mr.cookie'
version = '0.0.1'
sourceCompatibility = '17'

apply plugin: 'checkstyle'
apply from: "$rootDir/gradle/unitTest.gradle"
apply from: "$rootDir/gradle/integrationTest.gradle"

repositories {
    mavenCentral()
}

ext {
    apacheCollectionsVersion = "4.4"
    assertjVersion = "3.24.2"
    checkstyleVersion = "0.0.39"
    flywayVersion = "9.22.2"
    jetBrainsAnnotationVersion = "24.0.1"
    lombokVersion = "1.18.30"
    mapStructVersion = "1.5.5.Final"
    mysqlConnectorVersion = "8.0.33"
    snakeyamlVersion = "2.2"
    springDocVersion = "2.2.0"
    springFrameworkVersion = "6.0.12"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.apache.commons:commons-collections4:${apacheCollectionsVersion}"
    implementation "org.jetbrains:annotations:${jetBrainsAnnotationVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    implementation "org.flywaydb:flyway-mysql:${flywayVersion}"

    // transitive dep
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "org.springframework:spring-expression:${springFrameworkVersion}"
    implementation "org.springframework:spring-webmvc:${springFrameworkVersion}"

    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:${checkstyleVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "com.h2database:h2"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
tasks.withType(Test).configureEach {
    testLogging {
        events "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}
jacoco {
    toolVersion = "0.8.10"
}

sonarqube {
    properties {
        property "sonar.projectKey", "cookieMr_spring6udemy"
        property "sonar.organization", "cookiemr-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.withType(Checkstyle).configureEach {
    exclude "**/generated/**"
    exclude "**/sources/**"
}
checkstyle {
    toolVersion '10.10.0'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source = 'src/main/java'
}
