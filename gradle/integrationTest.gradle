sourceSets {
    integrationTest {
        java.srcDir "$projectDir/tests/integrationTest/java"
        resources.srcDir "$projectDir/tests/integrationTest/resources"

        compileClasspath += main.output + test.output + testUtils.output
        runtimeClasspath += main.output + test.output + testUtils.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

tasks.register('integrationTest', Test) {
    description = 'Runs Integration Tests with H2 Database.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    testLogging {
        events "skipped", "failed"
    }
}
